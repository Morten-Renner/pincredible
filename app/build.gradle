plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jmailen.kotlinter' version '4.3.0' // lintKotlin, formatKotlin
    id 'com.getkeepsafe.dexcount' version '4.0.0' // :app:countReleaseDexMethods
}

android {
    namespace 'com.cyb3rko.pincredible'
    compileSdk 34
    defaultConfig {
        applicationId 'com.cyb3rko.pincredible'
        minSdk 23
        targetSdk 34
        versionCode 12
        versionName '1.0.0a'
        resValue 'string', "app_name", "PINcredible DEBUG"
        signingConfig signingConfigs.debug
    }
    applicationVariants.configureEach { variant ->
        variant.resValue 'string', 'version_name', "Pre-Release v${variant.versionName}"
    }
    buildTypes {
        development {
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".dev"
            resValue 'string', "app_name", "PINcredible DEV"
        }
        release {
            minifyEnabled true
            shrinkResources true
            resValue 'string', "app_name", "PINcredible"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    bundle {
        storeArchive {
            enable = false
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.version']
        }
    }
    lint {
        disable 'Instantiatable'
    }
}

if (project.hasProperty('sign')) {
    android {
        signingConfigs {
            release {
                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWD')
                keyAlias System.getenv('KEYSTORE_KEY_ALIAS')
                keyPassword System.getenv('KEYSTORE_KEY_PASSWD')
            }
        }
    }
    android.buildTypes.release.signingConfig android.signingConfigs.release
}

dependencies {
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation project(path: ':backpack')
    developmentImplementation 'com.squareup.leakcanary:leakcanary-android:2.13'

//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
//    testImplementation 'junit:junit:4.13.2'
}
